grammar uk.ac.kcl.farm.Farm with org.eclipse.xtext.common.Terminals

generate farm "http://www.ac.uk/kcl/farm/Farm"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

FarmProgram:
	statements += (Class | Attribute)+
;

// General statement

Class:
	Crop |
	Field |
	Mission
;

Param:
	"crop" param=ID |
	"field" param=ID |
	"mission" param=ID |
	"real" param=ID |
	"string" param=ID
;

TypeName:
	"null" | "real" | "boolean" | "string"
;

Statement:
	Variable |
	LoopStatement |
	JudgeStatement |
	BuiltinFunction
;

Variable:
	"var" name=ID "=" value=Expression
;

LoopStatement:
	"while" "(" condition=Expression ")" "{"
		statements += Statement*
	"}"
;

JudgeStatement:
	"if" "(" condition=Expression ")" "{"
		statements += Statement*
	"}" elseif += ElseJudgeStatement*
	("else" "{" elseStatement += Statement* "}")?
;

ElseJudgeStatement:
	"elseif" "(" condition=Expression ")" "{"
		statements += Statement*
	"}"
;

BuiltinFunction:
	ReportFunction | 
	GetStageFunction |
	CountStageFunction |
	GetValueFunction |
	SetFieldValueFunction |
	PlantFunction | 
	MoveFunction |
	WaitFunction
;


Entity:
	Crop | Field | Variable 
;

ReportFunction hidden():
	entity=[Entity]  ".report(" ")"
;

GetStageFunction hidden():
	getStageCrop=[Crop | ID] ".getStage(" stageNumber=INT ")"
;

CountStageFunction hidden():
	countStageCrop=[Crop | ID] ".countStage(" ")"
;

GetValueFunction hidden():
	entity=[Entity] ".getValue(" attribute=STRING ")"
;

SetFieldValueFunction hidden():
	setValueField=[Field | ID] ".setFieldValue(" setFieldAttribute=STRING "," setFieldValue=AdditionExpression ")"
;

PlantFunction hidden():
	plantInField=[Field | ID] ".plant(" plantCrop=[Crop | ID] ")"
;

MoveFunction hidden():
	"move(" moveFromField=[Field | ID] ","  moveToField=[Field | ID] ")"
;

WaitFunction hidden():
	"wait(" value=AdditionExpression ")"
;

// MoveStatement:
// 	command=MoveCommand "(" steps=Addition ")"
// ;

// enum MoveCommand:
// 	forward | backward
// ;

// TurnStatement:
// 	"turn" command=TurnCommand "by" degrees=REAL "degrees"
// ;

// enum TurnCommand:
// 	left | right
// ;

//Expressions

Expression:
	ConditionOrExpression
;

ConditionOrExpression returns Expression:
	ConditionAndExpression => (
		{ConditionOrExpression.left=current} "||" right=ConditionAndExpression
	)*
;

ConditionAndExpression returns Expression:
	RelationOrExpression => (
		{ConditionAndExpression.left=current} "&&" right=RelationOrExpression
	)*
;

RelationOrExpression returns Expression:
	AdditionExpression ( => (
		{LessThanOrEqual.left=current} "<=" |
		{LessThan.left=current} "<" |
		{GreaterThanOrEqual.left=current} ">=" |
		{GreaterThan.left=current} ">" |
		{Equal.left=current} "==" |
		{NotEqual.left=current} "!="
	) right=AdditionExpression )* 
;

AdditionExpression returns Expression:
	MultiplicationExpression ( => (
		{Plus.left=current} "+" |
		{Minus.left=current} "-"
	) right+=MultiplicationExpression )*
;

MultiplicationExpression returns Expression:
	UnaryExpression ( => (
		{Multiply.left=current} "*" | 
		{Divide.left=current} "/" 
	) right+=UnaryExpression)*
;

UnaryExpression returns Expression:
	UnaryExpressionNotPlusMinus |
	({UnaryExpression} "-" exp=UnaryExpression) 
;

UnaryExpressionNotPlusMinus returns Expression:
	NotBooleanExpression | PrimaryExpression
;

NotBooleanExpression returns Expression:
	"!" exp=UnaryExpression
;

PrimaryExpression returns Expression:
	Literal |
	var=[Variable | ID] |
	"(" Expression ")"
;

Literal:
	BooleanLiteral | RealLiteral
;

BooleanLiteral:
	{BoolLiteral} val=BOOLEAN
;

terminal BOOLEAN returns ecore::EBoolean:
	"true" | "false"
;

RealLiteral:
	{RealLiteral} val=REAL
;

REAL returns ecore::EFloat hidden():
	INT? "." INT
;

// Define codes
Attribute:
	"define" name=ID
;

// Crop code block
Crop:
	"crop" name=ID "{"
		"name" ":" cropName=STRING
		"stage" ":" "["
			statements += CropStages
		"]"
	"}"
;

CropStages:
	elements += CropStage ("," elements += CropStage)* 
;

CropStage:
	"{"	
		"name" ":" name=STRING
		"timeConsumed" ":" timeConsumend=Expression
		elements += CropAttributes*
	"}"
;

CropAttributes:
	type=[Attribute | ID] ":" value=Expression
;

// Field code block
Field:
	"field" name=ID "{"
		"name" ":" fieldName=STRING
		"ip" ":" ip=IP
		"type" ":" fieldType=("inside" | "outside")
		"light" ":" fieldLight=("sunlight" | "LED")
		"monitor" ":" "[" fieldMonitors+=FieldMonitor ("," fieldMonitors+=FieldMonitor)* "]"
	"}"
;

IP hidden():
	(INT '.' (INT '.' (INT '.' (INT))))
;

FieldMonitor:
	monitor=[Attribute | ID]
;

// Main code block
Mission:
	"mission" name=ID "{"
		statements += (TaskStatement | ExecuteStatement)+
	"}"
;

// Task code block
TaskStatement:
	"task" name=ID "(" (parmas+=Param ("," parmas+=Param)*)? ")" ":" typeName=TypeName "{"
		statements += (Statement | ReturnStatement)*
	"}"
;

ReturnStatement:
	"return" "(" value=Expression ")" 
;

ExecuteStatement:
	"execute" "{"
		statements += Statement*
	"}"
;